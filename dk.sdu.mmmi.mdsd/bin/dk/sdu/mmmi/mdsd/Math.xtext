grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Math:
	"program" name=ID externals+=External* lines += MathExp+	
;

External:
	"external" name=ID "(" argsType+=ID? ("," argsType+=ID)* ")"
;


MathExp:
	'var' value=Assignment
;

Assignment:
	{Assignment} name=ID '=' exp=Exp
;

Exp returns Expression:
	SubAddExp
;
//Reverse PMDAS
SubAddExp returns Expression:
	DivMultExp (('-' {Subtraction .left=current} | '+' {Addition.left=current})  right=DivMultExp)*
;


DivMultExp returns Expression:
	Primary (('/' {Division.left=current} | '*' {Multiplication.left=current}) right=Primary)*
;

Primary returns Expression:
	Number | Parenthesis | VariableUse | Let | ExternalUse
;

Parenthesis returns Expression:
	{Parenthesis} '(' inner=Exp ')'
;

ExternalUse returns Expression:
	{ExternalUse} ref = [External] "(" parameters+=Exp? ("," parameters+=Exp)* ")"
;

Number returns Expression:
	{Number} value=INT
;

VariableUse returns Expression:
	{VarUse} ref = [Assignment]
;

Let :
	{Let} 'let' value=Assignment 'in' exp=Exp 'end'
;